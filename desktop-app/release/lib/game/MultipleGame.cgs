/*${cgsuite.banner}*/

class MultipleGame extends Game : java "org.cgsuite.lang.game.MultipleGame"

    property Multiplier.get : java "getMultiplier";
    property Multiplicand.get : java "getMultiplicand";
    
    override property IsImpartial.get
        return Multiplicand.IsImpartial;
    end
    
    override property CanonicalForm.get
        if Multiplier == 0 then
            return 0;
        else
            return Multiplier.NortonProduct(Multiplicand.CanonicalForm);
        end
    end
    
    override property NimValue.get
        m := Multiplicand.NimValue;
        if Multiplier % 2 == 0 then
            return 0;
        else
            return m;
        end
    end
    
    override method Options(Player player)
        if Multiplier < 0 then
            return ((-Multiplier) * (-Multiplicand)).Options(player);
        elseif Multiplier == 0 then
            return {};
        elseif Multiplier == 1 then
            return Multiplicand.Options(player);
        else
            h := (Multiplier-1) * Multiplicand;
            return setof(h + g for g in Multiplicand.Options(player));
        end
    end

    override property ToString.get : java "toString";

end
