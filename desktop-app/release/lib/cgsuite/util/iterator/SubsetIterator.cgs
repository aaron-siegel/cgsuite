/*${cgsuite.banner}*/

/*
 * SubsetIterator.cgs
 * 
 * Iterator for subsets of a given set. Takes one or two parameters, with just
 * one (a set or list) it iterates over all the subsets, in size order.
 * With two parameters, one an integer, the other a set or list (in either
 * order) it iterates over the subsets of that size. Note that if a list
 * argument has repeated elements, then these may occur up to their
 * multiplicities. However, the iterator will also return some duplicates in
 * this case, as they are treated as being distinct.
 *
 * Examples:
 * it := SubsetIterator([0, *, ^]);
 * while it.HasNext do Worksheet.Print(it.Next); end;
 *
 * Created by malbert on 4/08/2011
 */

mutable class SubsetIterator extends Iterator

    var valueList;
    var subsetSize;
    var indexSeq;
    var allSubsets;
    var iterators;
    
    method SubsetIterator(arg1?, arg2?) 
        
        if arg1 is Integer then
            subsetSize := arg1;
            vals := arg2;
        else
            vals := arg1;
            subsetSize := arg2;
        end
        
        allSubsets := not subsetSize is Integer;
        
        if vals is Collection then
            valueList := listof(x for x in vals);
        else
            error("SubsetIterator requires a collection.");
        end
        
        this.Reset();
   
    end
    
    override mutable property HasNext.get
       
        if not allSubsets then 
                return this.indexSeq != nil;
        end
       
        return iterators.HasNext;
       
    end
    
    override mutable property Next.get
    
        if allSubsets then
            return iterators.Next;
        end
    
        result := {};
        for i from 1 to this.indexSeq.Length do    
            result.Add(valueList[this.indexSeq[i]]);
        end
        
        for i from this.indexSeq.Length to 1 by -1 while this.indexSeq[i] == valueList.Length - this.indexSeq.Length + i do end;
        
        if i == 0 then
            this.indexSeq := nil;
        else
            this.indexSeq[i] := this.indexSeq[i] + 1;
            for j from i+1 to this.indexSeq.Length do
                this.indexSeq[j] := this.indexSeq[j-1] + 1;
            end
        end
        
        return result;
    end
    
    override mutable method Reset() 
        
        if allSubsets then
            iterators := ChainedIterator(listof(SubsetIterator(i, valueList) for i from 0 to valueList.Length));
        elseif subsetSize > valueList.Length or subsetSize < 0 then
            this.indexSeq := nil;
        else
            this.indexSeq := listof(i for i from 1 to subsetSize);
        end
        
    end

end