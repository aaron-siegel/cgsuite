CGScript is a powerful scripting language used in CGSuite for just about everything.  When you type commands into
the worksheet, you're writing them in CGScript.  But it can also be used for more complicated tasks, like
specifying the rules for new combinatorial games.

This chapter introduces the basic elements of CGScript.  A separate tutorial,
<a href=\"../usingcgscript/Using CGScript.html\">Using CGScript</a>, describes how to design new classes and
implement new games in CGScript.


This tutorial will introduce you to the basic elements of
CGScript, and in the next one we'll see how to use it to design new classes.

##TOC##

++ Sets ++

When you type, say,

$opts := *8.LeftOptions$

you get back all the Left options of $*8$.  The resulting ~set~ is itself an object and can be
    manipulated in various ways.  For example,

$opts.Add(*8)$

adds the new option $*8$ to the set.  Or

$opts.Contains(*3)$

returns true or false depending on whether $*3$ is in the set.  You can use these sets to construct
    games, as in

${ opts | opts }$

which returns the expected $*9$ (since we just added $*8$ to the set).

You can also specify sets explicitly, such as

${}$

for the empty set, or

${0,*,*2}$

for a set with elements.

Note that a pair of braces ${ }$ denotes a ~game~ if there's at least one slash in between, and a ~set~ otherwise.

++Lists and Maps++

A list is like a set, but the elements are ordered and can be duplicated.  Lists are created just like sets, but using
square brackets instead of curly braces.  For example,

$[0,*,*2]$

creates the list of $0$, $*$ and $*2$.  To appreciate the difference between lists and sets, try entering the following
two commands into the worksheet:

${1,5,1,3}$

$[1,5,1,3]$

If $x$ is a list, then you can access the n^th^ element with square brackets like so:

$x[2]$

or

$x[2] := 5$

Maps are another useful abstraction.  They associate keys to values (both of which can be any object).  The syntax
for constructing a map looks like this:

${ * => 1, *2 => 4, *3 => 9 }$

If $x$ is a list, then you can access the mapping with square brackets:

$x[*2]$

would return $4$ for the above example, and

$x[*4] := 16$

would assign the value $16$ to the key $*4$.

The special syntax

${ => }$

is used to denote an empty map (this distinguishes it from ${}$, which denotes an empty set).

++ Example: PartizanSubtraction ++

Here's an example that illustrates the use of collections in practice.  Let S_L_ and S_R_ be sets of positive
integers.  The ~Partizan Subtraction Game~ on (S_L_,S_R_) is played with heaps of tokens.  Left may remove ~k~ tokens
from a heap, for any ~k~ in S_L_.  Likewise Right may remove $k$ tokens for any $k$ in S_R_.

The CGSuite class $examples.PartizanSubtraction$ can represent ~any~ partizan subtraction game: the subtraction sets are
simply specified as parameters.  For example,

$g := examples.PartizanSubtraction({1,3},{2,3},10)$

constructs a partizan subtraction game with S_L_ = {1,3}, S_R_ = {2,3}, and heap size 10.  As always, you can type

$g.CanonicalForm$

to see the canonical form.

++ setof, listof, tableof, sumof ++

$setof$, $listof$, and $tableof$ are powerful commands for constructing new collections from existing ones.
The basic syntax looks like this:

$setof(g.CanonicalForm for g in x)$

If $x$ is an existing collection, this command returns the set of all canonical forms of games in $x$.  $listof$ is
similar, but gives the answer as a list instead of a set.  You can also use the syntax

$setof(k\^2 for k from 1 to 10)$

to iterate over integers spanning a certain range.  For example, the following is equivalent to $*n.LeftOptions$:

$setof(*k for k from 0 to n-1)$

The $tableof$ command can be useful for visualizing large amounts of data.  A ~table~ is another type of collection,
whose elements are themselves lists.  Each list in the table describes a ~row~ of the table.  Here's an example:

$tableof([n,examples.PartizanSubtraction({1,3},{2,3},n).CanonicalForm] for n from 1 to 10)$

This gives the first 10 values of the partizan subtraction game we introduced above.  As you can see (by typing it
into the worksheet), CGSuite prints tables in a neatly readable format.

++ Properties, Methods and Constructors ++

We've already seen that objects have various ~properties~ associated to them, which are accessible using "dot notation,"
such as

${5|1}.Temperature$

here $Temperature$ is a property of the object ${5|1}$.

Objects also have various functions associated to them, called ~methods~.  We've already seen several examples, such
as the $Cool$ method:

${5|1}.Cool(1)$

Methods themselves are objects, and can be assigned to variables too; for example

$f := {5|1}.Cool$

enables us to type

$f(1)$

or, say

$listof(f(k) for k from 0 to 2)$

Moreover, every class has a special method known as its ~constructor~.  The constructor is a rule for creating new
objects of that class.  When you type

$examples.PartizanSubtraction({1,3},{2,3},10)$

as in the example above, you're calling the constructor of the $examples.PartizanSubtraction$ class.  So even though
$examples.PartizanSubtraction$ is a class, it's identified with its constructor and can be used like a method.  In fact
we can type

$f := examples.PartizanSubtraction$

and then $f({1,3},{2,3},10)$ to get the same result.

There is also the handy $sumof$ keyword: for example,

$sumof(n\^2 for n from 1 to 5)$

gives the answer 1^2^ + 2^2^ + 3^2^ + 4^2^ + 5^2^ = 55.

++ Optional Parameters ++

A method takes a fixed number of parameters.  For $Cool$ this is just one; for $examples.PartizanSubtraction$, it's
three.  Some methods also have ~optional parameters~ that can be specified or omitted depending on the situation.  For
example, the $DayN$ class is used to construct all the games born by day ~n~.  Typing

$d := DayN(3)$

for example, gives all the games born by day 3, and you can now type (say)

$d.Size$

to get the number of such games (1474).  But the $DayN$ class also allows the optional parameter $allSmall$, which
restricts the computation just to all-small games.  The syntax for this is

$d := DayN(3, allSmall => true)$

which sets the optional parameter $allSmall$ to the value $true$ (the default, of course, is $false$).  Now typing
$d.Size$ gives the answer 67.

++ Procedures ++

In addition to methods and constructors, there is another type of function in CGSuite: procedures.  A ~procedure~ is a
simple rule that defines a mapping.  Unlike a map, it's an implicit rule rather than an explicit list of keys and values.
And unlike a method, it's not associated with any object (though the procedure is itself an object, like everything else).

An example of a procedure is

$x -> x+1$

If we type

$f := x -> x+1$

then $f(5)$ gives the expected answer, $6$.  Multi-argument procedures are allowed via the syntax

$(x,y) -> (x+y)/2$

For example, in versions of CGSuite prior to 1.0, the notation $C(g)$ was used to get the canonical form of $g$.  This
is no longer used in 1.0, but if you like it you can bring it back with a procedure definition:

$C := g -> g.CanonicalForm$

You can use similar aliases to create shortcuts for other commonly used commands.

++ Other Types ++

A few other types are worth mentioning.  Strings like

$"xox|ox."$

or

$"Hello there!"$

are objects of class $String$, and the special values $true$ and $false$ are objects of class $Boolean$.

Finally, there is the class $Nil$, which has a single object, $nil$.  A value of $nil$
is like saying "nothing": it's a universal default.  If you type $nil$ into the worksheet, you'll get no
output.

You can test whether any object $x$ matches a given type (say $String$) with the syntax

$x is String$

and you can get the exact type of $x$ by typing

$x.Class$

If you see an object you don't quite understand, often a good way to get a feel for what CGSuite is thinking is to ask
for its class.

++ Exercises ++
<ol>
<li>Consider the partizan subtraction game we studied above, ${1,3}$ vs. ${2,3}$.  Print the first 10 canonical forms just as we
did in the $tableof$ section above, and note that they rapidly become a mess.  Now print the first 30 ~reduced~ canonical
forms.  (You can do this by using the $Rcf$ property in place of $CanonicalForm$.)  Make a conjecture about the RCFs of
arbitrary positions.
</ol>