class Game : java "org.cgsuite.lang.Game"

    private static var placeholder := Object();

    public method op+ (Game obj) : java "buildSum";
    public method op- (Game obj)
        return this + (-obj);
    end
    public method op neg () : java "buildInverse";

    public property LeftOptions.get
        return this.Options(Player.Left);
    end
    public property RightOptions.get
        return this.Options(Player.Right);
    end

    public property CanonicalForm.get

        memory := this.Class.Memory;
        cg := memory[this];
        if nil != cg then
            if Game.placeholder == cg then
                error("Unable to compute canonical form: the specified game is loopy.");
            else
                return cg;
            end
        end;
        memory[this] := Game.placeholder;
        lo := this.LeftOptions;
        ro := this.RightOptions;
        clo := setof(g.CanonicalForm for g in lo);
        cro := setof(g.CanonicalForm for g in ro);
        cg := {clo|cro};
        memory[this] := cg;
        return cg;
        
    end

    public method SensibleOptions(Player player)

        opts := this.Options(player);
        copts := this.CanonicalForm.Options(player);
        sopts := {};

        for k in copts do
            for g in opts do
                if (player == Player.Left  and g.CanonicalForm >= k) or
                   (player == Player.Right and g.CanonicalForm <= k) then

                   sopts.Add(g);
                   break;

                end
            end
        end;

        return sopts;

    end

    public property SensibleLeftOptions.get
        return this.SensibleOptions(Player.Left);
    end

    public property SensibleRightOptions.get
        return this.SensibleOptions(Player.Right);
    end

end
