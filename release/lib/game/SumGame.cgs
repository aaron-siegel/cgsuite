/*${cgsuite.banner}*/

class SumGame extends Game : java "org.cgsuite.lang.game.SumGame"

    method SumGame(List components) : java "SumGame";

    override method op+ (Game obj) : java "add";
    
    override method op neg()
        return sumof(-g for g in Components);
    end
    
    property Components.get : java "getComponents";
    
    override property IsImpartial.get
    
        for g in Components do
            if not g.IsImpartial then
                return false;
            end
        end
        return true;
    
    end
    
    override property CanonicalForm.get
    
        cg := 0;
        for g in Components do
            cg := cg + g.CanonicalForm;
        end
        return cg;
        
    end
    
    override property NimValue.get
    
        m := 0;
        for g in Components do
            m := m.NimSum(g.NimValue);
        end
        return m;
    
    end
    
    override method Options(Player player)
    
        opts := {};
        components := Components;
        
        for n from 1 to components.Length do
            for g in components[n].Options(player) do
                
                copy := components;
                copy[n] := g;
                opts.Add(SumGame(copy));
                
            end
        end
        
        return opts;
    
    end
    
    override property ToString.get : java "toString";

end
