/*${cgsuite.banner}*/

class BasicMancala extends Game

    var l, r;

    method BasicMancala(List l, List r)
    end
    
    override method Options(Player player)
    
        if player == Player.Left then
            us := l; them := r;
        else
            us := r; them := l;
        end;
        
        options := {};
        
        for k from 1 to us.Length do
        
            if us[k] > 0 then
                newUs := us;
                newThem := them;
                t := us[k];
                newUs[k] := 0;
                curpos := k;
                while t > 0 do
                    t := t-1;
                    curpos := curpos+1;
                    if curpos > newUs.Length + newThem.Length + 2 then
                        curpos := 1;
                    end;
                    
                    if curpos <= newUs.Length then
                        newUs[curpos] := newUs[curpos] + 1;
                    elseif curpos >= newUs.Length + 2 and curpos <= newUs.Length + 1 + newThem.Length then
                        idx := newUs.Length + 2 + newThem.Length - curpos;
                        newThem[idx] := newThem[idx] + 1;
                    end
                end;
                if player == Player.Left then
                    options.Add(BasicMancala(newUs, newThem));
                else
                    options.Add(BasicMancala(newThem, newUs));
                end
            end;
        
        end;
            
        return options;
    
    end
    
    override property ToString.get
    
        return "BasicMancala(" + l.ToString + "," + r.ToString + ")";
    
    end

end