/*
 * RookRace.cgs
 *
 * Rook Race is played on a grid with black and white rooks, vacant spaces, and blocked spaces. 
 * A move for Left is to move one of the black rooks at least one square, stopping when blocked by
 * the board edge, another rook, or a blocked space (no captures are made). After a move all the
 * squares from where the move began to just before where it finished are blocked.
 *
 * In an alternative version, short moves are allowed, i.e. the rook can stop before being blocked.
 *
 * Examples:
 * g := game.grid.RookRace("x...x|.....|.....|.....|o...."); (Long moves version)
 * g := game.grid.RookRace("x...x|.....|.....|.....|o....", true); (Short moves version)
 * 
 * Created by malbert on 28/07/2011
 */

class RookRace extends GridGame

    var shortMoves;

    method RookRace(grid, shortMoves ? false)
        this.GridGame(grid);
    end

    override method Options(Player player)

        options := {};

        us := player.Ordinal;
        
        for m from 1 to grid.RowCount do
            for n from 1 to grid.ColumnCount do
                if grid[m,n] == us then
                    for d in Direction.Orthogonal do
                        gap := 1;
                        while grid[m + gap*d.RowShift, n + gap*d.ColumnShift] == 0 do
                            if shortMoves or grid[m + (gap+1)*d.RowShift, n + (gap+1)*d.ColumnShift] != 0 then
                                copy := grid;
                                copy[m + gap*d.RowShift, n + gap*d.ColumnShift] := us;
                                g := gap-1;
                                while g >= 0 do
                                    copy[m + g*d.RowShift, n + g*d.ColumnShift] := 3;
                                    g := g-1;
                                end
                                options.Add(RookRace(copy, shortMoves));
                            end
                            gap := gap+1;
                        end
                    end
                end
            end
        end

        return options;

    end
    
    override property CanonicalForm.get

        components := grid.Decompose(3);
        g := 0;
        for component in components do
            g := g + RookRace(component.SymmetryInvariant(Symmetry.Reflection), shortMoves).super.CanonicalForm;
        end
        return g;

    end
    
    override property CharMap.get
        return ".xo#";
    end
    
    override property Icons.get
        return
        [
            GridIcon.Blank,
            GridIcon.BlackRook,
            GridIcon.WhiteRook,
            GridIcon.GraySquare
        ];
    end

end