class Clobber extends GridGame

    var directions;

    method Clobber(grid, Directions ? Direction.Orthogonal)
    
        this.GridGame(grid);
        this.directions := Directions;
        
    end
    
    method Options(Player player)

        options := {};

        us := player.Ordinal;
        them := player.Opponent.Ordinal;

        for m from 1 to this.grid.NumRows do
            for n from 1 to this.grid.NumColumns do
                if this.grid[m,n] == us then

                    for d in this.directions do
                        if this.grid[m+d.RowShift,n+d.ColumnShift] == them then
                            copy := this.grid;
                            copy[m,n] := 0;
                            copy[m+d.RowShift,n+d.ColumnShift] := us;
                            options.Add(Clobber(copy, this.directions));
                        end
                    end

                end
            end
        end;

        return options;

    end

    property CharMap.get
        return ".xo";
    end
    
    property Icons.get
        return
        [
            GridIcon.Blank,
            GridIcon.BlackStone,
            GridIcon.WhiteStone
        ];
    end

end