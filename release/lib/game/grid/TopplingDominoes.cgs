/*
 * TopplingDominoes.cgs
 *
 * Created by asiegel on Jul 13, 2011
 */

class TopplingDominoes extends GridGame

    method TopplingDominoes(grid)
    
        this.GridGame(grid);
        if grid.NumRows != 1 then
            error("TopplingDominoes grid must be 1 x N.");
        end
        
    end
    
    method Options(Player player)
        
        options := {};
        
        for n from 1 to grid.NumColumns do
        
            if grid[1,n] == player.Ordinal or grid[1,n] == 3 then
            
                options.Add(TopplingDominoes(grid.Subgrid(1, 1, 1, n-1)));
                options.Add(TopplingDominoes(grid.Subgrid(1, 1, n+1, grid.NumColumns)));
            
            end
        
        end
        
        return options;
        
    end

    override property CharMap.get
        return ".xoe";
    end
    
    override property Icons.get
        return
        [
            GridIcon.Blank,
            GridIcon.BlackDomino,
            GridIcon.WhiteDomino,
            GridIcon.GrayDomino
        ];
    end
    
end