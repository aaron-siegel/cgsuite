/*
 * TopplingDominoes.cgs
 *
 * Created by asiegel on Jul 13, 2011
 */

class TopplingDominoes extends GridGame

    method TopplingDominoes(Grid grid)
    
        this.GridGame(grid);
        if this.grid.NumRows != 1 then
            error("TopplingDominoes grid must be 1 x N.");
        end
        
    end
    
    method Options(Player player)
        
        options := {};
        
        for n from 1 to this.grid.NumColumns do
        
            if this.grid[1,n] == player.Ordinal or this.grid[1,n] == 3 then
            
                options.Add(TopplingDominoes(this.grid.Subgrid(1, 1, 1, n-1)));
                options.Add(TopplingDominoes(this.grid.Subgrid(1, 1, n+1, this.grid.NumColumns)));
            
            end
        
        end
        
        return options;
        
    end

    property CharMap.get
        return ".xoe";
    end
    
    property Icons.get
        return
        [
            GridIcon.Blank,
            GridIcon.BlackStone,
            GridIcon.WhiteStone,
            GridIcon.Grayed
        ];
    end
    
end