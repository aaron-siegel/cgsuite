class Amazons extends GridGame

    method Amazons(grid)
        this.GridGame(grid);
    end

    method Options(Player player)

        options := {};

        us := player.Ordinal;

        for m from 1 to grid.RowCount do
            for n from 1 to grid.ColumnCount do
                if grid[m,n] == us then

                    for d in Direction.All do
                        for k from 1 to grid.RowCount do
                            m2 := m + d.RowShift * k;
                            n2 := n + d.ColumnShift * k;
                            if grid[m2,n2] == 0 then
                                for e in Direction.All do
                                    for l from 1 to grid.RowCount do
                                        mArrow := m2 + e.RowShift * l;
                                        nArrow := n2 + e.ColumnShift * l;
                                        if (grid[mArrow,nArrow] == 0) or (mArrow == m and nArrow == n) then
                                            copy := grid;
                                            copy[m,n] := 0;
                                            copy[m2,n2] := us;
                                            copy[mArrow,nArrow] := 3;
                                            options.Add(Amazons(copy));
                                        else
                                            break;
                                        end
                                    end
                                end
                            else
                                break;
                            end
                        end
                    end

                end
            end
        end

        return options;

    end
    
    override property CharMap.get
        return ".xo#";
    end
    
    override property Icons.get
        return
        [
            GridIcon.Blank,
            GridIcon.BlackQueen,
            GridIcon.WhiteQueen,
            GridIcon.BlackStone
        ];
    end

end