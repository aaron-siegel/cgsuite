class Push extends GridGame

    method Push(grid)
        this.GridGame(grid);
        if grid.NumRows != 1 then
            error("Push grid must be 1 x N.");
        end;
    end

    method Options(Player player)

        options := {};

        us := player.Ordinal;
        them := player.Opponent.Ordinal;

        for n from 1 to grid.NumColumns do
            if grid[1,n] == us then
                copy := grid.Copy();
                for k from n-1 to 1 by -1 while copy[1,k] != 0 do end;
                if k == 0 then k := 1; end;
                for k to n-1 do
                    copy[1,k] := copy[1,k+1];
                end;
                copy[1,n] := 0;
                options.Add(Push(copy));
            end
        end;

        return options;

    end
    
    override property CharMap.get
        return ".xo";
    end
    
    override property Icons.get
        return
        [
            GridIcon.Blank,
            GridIcon.BlackStone,
            GridIcon.WhiteStone
        ];
    end

end