/*
 * Fission.cgs
 *
 * Fission is played in a grid populated by black stones, empty cells,
 * and possibly 'blocked' cells (represented by black squares). A move
 * for Left consists of taking a black stone which has empty spaces both
 * above and below it, placing two new stones in those spaces and then
 * removing the original stone. Right moves similarly in a horizontal
 * direction.
 
 * Examples:
 * F := game.grid.Fission;
 * g := F(".x..|x.x.|.#..");
 * g.CanonicalForm; (should be {1 | 0})
 *
 * Created by MichaelAlbert on Aug 24, 2011
 */

class Fission extends GridGame

    method Fission(grid)
        this.GridGame(grid);
    end
    
    override method Options(Player player)

        options := {};

        if player == Player.Left then
            ds := Direction.Vertical
        else
            ds := Direction.Horizontal
        end

        for m from 1 to grid.RowCount do
            for n from 1 to grid.ColumnCount do
                if grid[m,n] == 1 then

                    for d in ds do
                        if grid[m-  d.RowShift,n-  d.ColumnShift] == 0 and
                           grid[m+  d.RowShift,n+  d.ColumnShift] == 0 then
                            copy := grid;
                            copy[m,n] := 0;
                            copy[m+  d.RowShift,n+  d.ColumnShift] := 1;
                            copy[m-  d.RowShift,n-  d.ColumnShift] := 1;
                            options.Add(Fission(copy));
                        end
                    end

                end
            end
        end

        return options;

    end
    
    override property CharMap.get
        return ".x#";
    end
    
    override property Icons.get
        return
        [
            GridIcon.Blank,
            GridIcon.BlackStone,
            GridIcon.BlackSquare
         ];
    end

end