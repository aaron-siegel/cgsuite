class ToadsAndFrogs extends GridGame

    var backsliding;

    method ToadsAndFrogs(grid, backsliding ? false)
        this.GridGame(grid);
        if this.grid.NumRows != 1 then
            error("ToadsAndFrogs grid must be 1 x N.");
        end;
        this.backsliding := backsliding;
    end

    method Options(Player player)

        options := {};

        us := player.Ordinal;
        them := player.Opponent.Ordinal;

        for n from 1 to this.grid.NumColumns do
            if this.grid[1,n] == us then

                if this.grid[1,n+player.Sign] == 0 then
                    copy := this.grid;
                    copy[1,n] := 0;
                    copy[1,n+player.Sign] := us;
                    options.Add(ToadsAndFrogs(copy, this.backsliding));
                elseif this.grid[1,n+player.Sign] == them and this.grid[1,n+2*player.Sign] == 0 then
                    copy := this.grid;
                    copy[1,n] := 0;
                    copy[1,n+2*player.Sign] := us;
                    options.Add(ToadsAndFrogs(copy, this.backsliding));
                end;

                if this.backsliding and this.grid[1,n-player.Sign] == 0 then
                    copy := this.grid;
                    copy[1,n] := 0;
                    copy[1,n-player.Sign] := us;
                    options.Add(ToadsAndFrogs(copy, this.backsliding));
                end

            end
        end;

        return options;

    end

    property CharMap.get
        return ".tf";
    end
    
    property Icons.get
        return
        [
            GridIcon.Blank,
            GridIcon.Toad,
            GridIcon.Frog
        ];
    end

    /*
    property CanonicalForm.get

        // Check the death leap principle (Erickson's criterion).

        deathLeap := true;
        for n from 1 to this.grid.NumColumns-1 do
            if this.grid[1,n] == 1 and this.grid[1,n+1] == 0 or
               this.grid[1,n] == 0 and this.grid[1,n+1] == 2 or
               this.grid[1,n] == 1 and this.grid[1,n+1] == 2 and this.grid[1,n+2] == 0 and this.grid[1,n+3] == 0 or
               this.grid[1,n] == 0 and this.grid[1,n+1] == 0 and this.grid[1,n+2] == 1 and this.grid[1,n+3] == 2 then

                deathLeap := false;
                break;

            end
        end;

        if deathLeap then
            return 0;
        end;

        return super.CanonicalForm;

    end
    */

end