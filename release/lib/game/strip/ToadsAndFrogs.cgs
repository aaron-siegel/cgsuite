class ToadsAndFrogs extends StripGame

    var backsliding;

    method ToadsAndFrogs(grid, backsliding ? false)
        this.StripGame(grid);
    end

    method Options(Player player)

        options := {};

        us := player.Ordinal;
        them := player.Opponent.Ordinal;

        for n from 1 to grid.ColumnCount do
            if grid[n] == us then

                if grid[n+player.Sign] == 0 then
                    copy := grid;
                    copy[n] := 0;
                    copy[n+player.Sign] := us;
                    options.Add(ToadsAndFrogs(copy, backsliding));
                elseif grid[n+player.Sign] == them and grid[n+2*player.Sign] == 0 then
                    copy := grid;
                    copy[n] := 0;
                    copy[n+2*player.Sign] := us;
                    options.Add(ToadsAndFrogs(copy, backsliding));
                end;

                if backsliding and grid[n-player.Sign] == 0 then
                    copy := grid;
                    copy[n] := 0;
                    copy[n-player.Sign] := us;
                    options.Add(ToadsAndFrogs(copy, backsliding));
                end

            end
        end

        return options;

    end

    override property CharMap.get
        return ".tf";
    end
    
    override property Icons.get
        return
        [
            GridIcon.Blank,
            GridIcon.Toad,
            GridIcon.Frog
        ];
    end

    /*
    property CanonicalForm.get

        // Check the death leap principle (Erickson's criterion).

        deathLeap := true;
        for n from 1 to grid.ColumnCount-1 do
            if grid[n] == 1 and grid[n+1] == 0 or
               grid[n] == 0 and grid[n+1] == 2 or
               grid[n] == 1 and grid[n+1] == 2 and grid[n+2] == 0 and grid[n+3] == 0 or
               grid[n] == 0 and grid[n+1] == 0 and grid[n+2] == 1 and grid[n+3] == 2 then

                deathLeap := false;
                break;

            end
        end;

        if deathLeap then
            return 0;
        end;

        return super.CanonicalForm;

    end
    */

end