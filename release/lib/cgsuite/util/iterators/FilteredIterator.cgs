/*
 * FilteredIterator.cgs
 *
 * A filtered iterator is built from an ordinary iterator and a filter
 * procedure, which should return true or false for each element produced
 * by the iterator.
 *
 * The filtered iterator returns successively those values generated by the
 * iterator for which the value of the filter procedure is true.
 *
 * Example: (print all 3x2 Clobber positions whose value is positive)
 * C := game.grid.Clobber;
 * clobIt := cgsuite.util.iterators.GridGameIterator(C, 3, 2, [0,1,2]);
 * filtIt := cgsuite.util.iterators.FilteredIterator(clobIt, g -> g.CanonicalForm > 0);
 * filtIt.TraverseWith(Worksheet.Print);
 *
 * Created by malbert on 9/08/2011
 */

mutable class FilteredIterator extends Iterator

    var iterator;
    var filters;
    var nextCandidate;
    var flag;
    
    method FilteredIterator(Iterator iterator, filters ...)
        fireIterator();
    end
    
    override mutable property HasNext.get
        if not flag then
            return false;
        end
        if Filter(nextCandidate) then
            return true;
        end
        fireIterator();
        return this.HasNext;
     end
     
     override mutable property Next.get
        while not Filter(nextCandidate) do
            fireIterator();
        end
        result := nextCandidate;
        fireIterator();
        return result;
     end
     
     override mutable method Reset() 
        iterator.Reset();
        fireIterator();
     end
     
     mutable method fireIterator()
        flag := iterator.HasNext;
        if flag then
            nextCandidate := iterator.Next;
        end
     end
     
     method Filter(candidate)
        for filter in filters do
            if not filter(candidate) then
                return false;
            end;
        end;
        return true;
     end
     
end