class Object : java "org.cgsuite.lang.CgsuiteObject"

    property Class.get : java "getCgsuiteClass";

    method Object()
    end

    method op==(key)  return this === key; end
    method op!=(key)  return not this == key; end
    method op>=(key)  return key <= this; end
    method op<(key)   return this <= key and not key <= this; end
    method op>(key)   return not this <= key and key <= this; end
    method op<>(key)  return not this <= key and not key <= this; end

    method op<=>(key)
        if this <= key then
            if key <= this then
                return 0;
            else
                return -1;
            end
        else
            if key <= this then
                return 1;
            else
                return *;
            end
        end
    end

    property ToString.get

        return "<Object of type " + this.Class.Name + ">";

    end

end
