/*${cgsuite.banner}*/

class Integer extends DyadicRational : java "org.cgsuite.lang.CgsuiteInteger"

    override method op+(Integer n) : java "add";
    override method op-(Integer n) : java "subtract";
    override method op*(Game g) : java "multiply";
    override method op neg () : java "negate";
    
    method NimSum(Integer n) : java "nimSum";
    method NimProduct(Integer n) : java "nimProduct";
    method Div(Integer n) : java "div";
    
    property Is2Power.get : java "is2Power";
    property IsSmall.get : java "isSmall";
    property Lb.get : java "lb";
        
    override property IsInteger.get
        return true;
    end
    
    static method Random(Integer obj) : java "random";
    static method SetSeed(Integer seed) : java "setSeed";
    
    property Isqrt.get
    
        if this == 0 then
            return 0;
        end

        a := Lb + 1;
        x := 2^(a.Div(2) + a%2);
        do
            y := (x + Div(x)).Div(2);
            if y >= x then
                return x;
            end
            x := y;
        end

    end

    override method Options(Player player)
        if player == Player.Left then
            return LeftOptions;
        else
            return RightOptions;
        end
    end
    
    override property LeftOptions.get : java "getLeftOptions";
    override property RightOptions.get : java "getRightOptions";

end
