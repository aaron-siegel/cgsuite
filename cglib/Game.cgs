class Game : java "org.cgsuite.lang.Game"

    private static var placeholder := Object();

    public static var memory := {=>};

    public method op+ (Game obj) : java "buildSum";
    public method op- (Game obj)
        return this + (-obj);
    end
    public method op neg () : java "buildInverse";

    public property LeftOptions.get
        return this.Options(Player.Left);
    end
    public property RightOptions.get
        return this.Options(Player.Right);
    end

    public property CanonicalForm.get

        cg := Game.memory[this];
        if nil != cg then
            if Game.placeholder == cg then
                error("Unable to compute canonical form: the specified game is loopy.");
            else
                return cg;
            end
        end;
        Game.memory[this] := Game.placeholder;
        lo := this.LeftOptions;
        ro := this.RightOptions;
        clo := setof(g.CanonicalForm for g in lo);
        cro := setof(g.CanonicalForm for g in ro);
        cg := {clo|cro};
        Game.memory[this] := cg;
        return cg;
        
    end

end
