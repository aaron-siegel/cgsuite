class Clobber extends Game : java "org.cgsuite.lang.Game"

    private var grid;

    public method Clobber(grid)
        if grid.Class == String then
            this.grid := Grid.ParseGrid(grid, ".xo");
        elseif grid.Class == Grid then
            this.grid := grid;
        end
        // TODO Error otherwise
    end

    public method Options(Player player)

        options := {};

        us := player.Ordinal;
        them := player.Opponent.Ordinal;

        for m from 1 to this.grid.NumRows do
            for n from 1 to this.grid.NumColumns do
                if this.grid[m,n] == us then

                    for d in Direction.Orthogonal do
                        if this.grid[m+d.RowShift,n+d.ColumnShift] == them then
                            copy := this.grid.Copy();
                            copy[m,n] := 0;
                            copy[m+d.RowShift,n+d.ColumnShift] := us;
                            options.Add(Clobber(copy));
                        end
                    end

                end
            end
        end;

        return options;

    end

    public property ToString.get
        return "Clobber(" + this.grid.MapString(".xo") + ")";
    end

end