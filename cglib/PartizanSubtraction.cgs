class PartizanSubtraction extends Game

    private var leftSet;
    private var rightSet;
    private var n;

    public method PartizanSubtraction(Set leftSet, Set rightSet, Number n)

        this.leftSet := leftSet;
        this.rightSet := rightSet;
        this.n := n;

    end

    public method Options(Player player)

        options := {};
        moveSet := if player == Player.Left then this.leftSet else this.rightSet end;

        for k in moveSet do

            if this.n - k >= 0 then
                options.Add(PartizanSubtraction(this.leftSet, this.rightSet, this.n - k));
            end

        end;

        return options;

    end

    public property ToString.get

        return "PartizanSubtraction(" + this.leftSet.ToString + "," + this.rightSet.ToString + "," + this.n.ToString + ")";

    end

end