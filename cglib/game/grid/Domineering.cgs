class Domineering extends GridGame

    private static var Icons := [
        GridIcon.Blank,
        GridIcon.Grayed
        ];

    private var grid;

    public method Domineering(grid)
        if grid.Class == String then
            this.grid := Grid.ParseGrid(grid, ".#");
        elseif grid.Class == Grid then
            this.grid := grid;
        end
        // TODO Error otherwise
    end

    public method Options(Player player)

        options := {};

        us := player.Ordinal;

        if player == Player.Left then
            mDelta := 1; nDelta := 0;
        else
            mDelta := 0; nDelta := 1;
        end;

        for m from 1 to this.grid.NumRows do
            for n from 1 to this.grid.NumColumns do
                if this.grid[m,n] == 0 and this.grid[m+mDelta,n+nDelta] == 0 then
                    copy := this.grid.Copy();
                    copy[m,n] := us;
                    copy[m+mDelta,n+nDelta] := us;
                    options.Add(Domineering(copy));
                end
            end
        end;

        return options;

    end

    public property ToString.get
        return "Domineering(" + this.grid.MapString(".#") + ")";
    end
end